#######################################################################
# Test for copying block of size 1;
#######################################################################
	.pos 0
main:	irmovq Stack, %rsp  	# Set up stack pointer

	# Set up arguments for copy function and then invoke it
	irmovq $1, %rdx		# src and dst have 1 elements
	irmovq dest, %rsi	# dst array
	irmovq src, %rdi	# src array
	call ncopy		 
	call check	        # Call checker code
	halt                    # should halt with 0xaaaa in %rax
StartFun:
#/* $begin ncopy-ys */
##################################################################
# ncopy.ys - Copy a src block of len words to dst.
# Return the number of positive words (>0) contained in src.
#
# Student: Zuo Yilong (1120231863)
#
# Optimizations:
# 1. Hybrid path: scalar loop for tiny blocks, 8-way unrolling for the rest.
# 2. Grouped loads avoid load-use hazards in the unrolled core.
# 3. Branchless positive counting (cmovg) minimizes mispredictions.
# 4. Pointer updates use iaddq to reduce instruction count.
#
##################################################################
# Do not modify this portion
# Function prologue.
# %rdi = src, %rsi = dst, %rdx = len
ncopy:

##################################################################
# You can modify this portion
	# Loop header
	xorq %rax,%rax
	andq %rdx,%rdx
	jle Done

	irmovq $8, %r8
	rrmovq %rdx, %r9
	subq %r8, %r9
	jl Small
	rrmovq %r9, %rdx

	pushq %r12
	irmovq $1, %r12		# constant 1 for branchless counting

Loop8:
	# First four loads
	mrmovq (%rdi), %r8
	mrmovq 8(%rdi), %r9
	mrmovq 16(%rdi), %r10
	mrmovq 24(%rdi), %r11

	# Store & count first four
	rmmovq %r8, (%rsi)
	xorq %rcx, %rcx
	andq %r8, %r8
	cmovg %r12, %rcx
	addq %rcx, %rax

	rmmovq %r9, 8(%rsi)
	xorq %rcx, %rcx
	andq %r9, %r9
	cmovg %r12, %rcx
	addq %rcx, %rax

	rmmovq %r10, 16(%rsi)
	xorq %rcx, %rcx
	andq %r10, %r10
	cmovg %r12, %rcx
	addq %rcx, %rax

	rmmovq %r11, 24(%rsi)
	xorq %rcx, %rcx
	andq %r11, %r11
	cmovg %r12, %rcx
	addq %rcx, %rax

	# Next four loads
	mrmovq 32(%rdi), %r8
	mrmovq 40(%rdi), %r9
	mrmovq 48(%rdi), %r10
	mrmovq 56(%rdi), %r11

	# Store & count next four
	rmmovq %r8, 32(%rsi)
	xorq %rcx, %rcx
	andq %r8, %r8
	cmovg %r12, %rcx
	addq %rcx, %rax

	rmmovq %r9, 40(%rsi)
	xorq %rcx, %rcx
	andq %r9, %r9
	cmovg %r12, %rcx
	addq %rcx, %rax

	rmmovq %r10, 48(%rsi)
	xorq %rcx, %rcx
	andq %r10, %r10
	cmovg %r12, %rcx
	addq %rcx, %rax

	rmmovq %r11, 56(%rsi)
	xorq %rcx, %rcx
	andq %r11, %r11
	cmovg %r12, %rcx
	addq %rcx, %rax

	iaddq $64, %rdi
	iaddq $64, %rsi
	iaddq $-8, %rdx
	jge Loop8

	iaddq $8, %rdx		# restore remainder
	jle LargeCleanup

Tail:
	mrmovq (%rdi), %r8
	rmmovq %r8, (%rsi)
	xorq %rcx, %rcx
	andq %r8, %r8
	cmovg %r12, %rcx
	addq %rcx, %rax
	iaddq $8, %rdi
	iaddq $8, %rsi
	iaddq $-1, %rdx
	jg Tail

LargeCleanup:
	popq %r12
	jmp Done

Small:
	jle Done

SmallLoop:
	mrmovq (%rdi), %r8
	rmmovq %r8, (%rsi)
	andq %r8, %r8
	jle SmallSkip
	iaddq $1, %rax
SmallSkip:
	iaddq $8, %rdi
	iaddq $8, %rsi
	iaddq $-1, %rdx
	jg SmallLoop
	jmp Done
##################################################################
# Do not modify the following section of code
# Function epilogue.
Done:
	ret
##################################################################
# Keep the following label at the end of your function
End:
#/* $end ncopy-ys */
EndFun:
#################################################################### 
# Epilogue code for the correctness testing driver
####################################################################

# This is the correctness checking code.
# It checks:
#   1. %rax has 0.  Set %rax to 0xbbbb if not.
#   2. The total length of the code is less than or equal to 1000.
#      Set %rax to 0xcccc if not.
#   3. The source data was copied to the destination.
#      Set %rax to 0xdddd if not.
#   4. The words just before and just after the destination region
#      were not corrupted.  Set %rax to 0xeeee if not.
# If all checks pass, then sets %rax to 0xaaaa
check:
	# Return value test
	irmovq $0,%r10
	subq %r10,%rax
	je checkb
	irmovq $0xbbbb,%rax  # Failed test #1
	jmp cdone
checkb:
	# Code length check
	irmovq EndFun,%rax
	irmovq StartFun,%rdx
	subq %rdx,%rax
	irmovq $1000,%rdx
	subq %rax,%rdx
	jge checkm
	irmovq $0xcccc,%rax  # Failed test #2
	jmp cdone
checkm:
	irmovq dest, %rdx # Pointer to next destination location
	irmovq src,%rbx   # Pointer to next source location
	irmovq $1,%rdi  # Count
	andq %rdi,%rdi
	je checkpre         # Skip check if count = 0
mcloop:
	mrmovq (%rdx),%rax
	mrmovq (%rbx),%rsi
	subq %rsi,%rax
	je  mok
	irmovq $0xdddd,%rax # Failed test #3
	jmp cdone
mok:
	irmovq $8,%rax
	addq %rax,%rdx	  # dest ++
	addq %rax,%rbx    # src++
	irmovq $1,%rax
	subq %rax,%rdi    # cnt--
	jg mcloop
checkpre:
	# Check for corruption
	irmovq Predest,%rdx
	mrmovq (%rdx), %rax  # Get word before destination
	irmovq $0xbcdefa, %rdx
	subq %rdx,%rax
	je checkpost
	irmovq $0xeeee,%rax  # Failed test #4
	jmp cdone
checkpost:
	# Check for corruption
	irmovq Postdest,%rdx
	mrmovq (%rdx), %rax  # Get word after destination
	irmovq $0xdefabc, %rdx
	subq %rdx,%rax
	je checkok
	irmovq $0xeeee,%rax # Failed test #4
	jmp cdone
checkok:
	# Successful checks
	irmovq $0xaaaa,%rax
cdone:
	ret

###############################
# Source and destination blocks 
###############################
	.align 8
src:
	.quad -1
	.quad 0xbcdefa # This shouldn't get moved

	.align 16
Predest:
	.quad 0xbcdefa
dest:
	.quad 0xcdefab
Postdest:
	.quad 0xdefabc

.align 8
# Run time stack
	.quad 0
	.quad 0
	.quad 0
	.quad 0
	.quad 0
	.quad 0
	.quad 0
	.quad 0
	.quad 0
	.quad 0
	.quad 0
	.quad 0
	.quad 0
	.quad 0
	.quad 0
	.quad 0

Stack:
