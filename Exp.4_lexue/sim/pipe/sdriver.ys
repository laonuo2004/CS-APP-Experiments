#######################################################################
# Test for copying block of size 4;
#######################################################################
	.pos 0
main:	irmovq Stack, %rsp  	# Set up stack pointer

	# Set up arguments for copy function and then invoke it
	irmovq $4, %rdx		# src and dst have 4 elements
	irmovq dest, %rsi	# dst array
	irmovq src, %rdi	# src array
	call ncopy		 
	halt			# should halt with num nonzeros in %rax
StartFun:
#/* $begin ncopy-ys */
##################################################################
# ncopy.ys - Copy a src block of len words to dst.
# Return the number of positive words (>0) contained in src.
#
# Student: Zuo Yilong (1120231863)
#
# Optimizations:
# 1. 8x loop unrolling with load-use hazard avoidance
# 2. Compact binary jump tree for 0-7 remainder
# 3. iaddq for all counter updates
#
##################################################################
# Do not modify this portion
# Function prologue.
# %rdi = src, %rsi = dst, %rdx = len
ncopy:

##################################################################
# You can modify this portion
	xorq %rax, %rax
	iaddq $-8, %rdx
	jl Rem

L0:
	mrmovq (%rdi), %r8
	mrmovq 8(%rdi), %r9
	rmmovq %r8, (%rsi)
	andq %r8, %r8
	jle L1
	iaddq $1, %rax
L1:
	mrmovq 16(%rdi), %r8
	rmmovq %r9, 8(%rsi)
	andq %r9, %r9
	jle L2
	iaddq $1, %rax
L2:
	mrmovq 24(%rdi), %r9
	rmmovq %r8, 16(%rsi)
	andq %r8, %r8
	jle L3
	iaddq $1, %rax
L3:
	mrmovq 32(%rdi), %r8
	rmmovq %r9, 24(%rsi)
	andq %r9, %r9
	jle L4
	iaddq $1, %rax
L4:
	mrmovq 40(%rdi), %r9
	rmmovq %r8, 32(%rsi)
	andq %r8, %r8
	jle L5
	iaddq $1, %rax
L5:
	mrmovq 48(%rdi), %r8
	rmmovq %r9, 40(%rsi)
	andq %r9, %r9
	jle L6
	iaddq $1, %rax
L6:
	mrmovq 56(%rdi), %r9
	rmmovq %r8, 48(%rsi)
	andq %r8, %r8
	jle L7
	iaddq $1, %rax
L7:
	rmmovq %r9, 56(%rsi)
	andq %r9, %r9
	jle Nxt
	iaddq $1, %rax
Nxt:
	iaddq $64, %rdi
	iaddq $64, %rsi
	iaddq $-8, %rdx
	jge L0

# Remainder: rdx is in [-8, -1], meaning 0-7 elements left
# iaddq $8 to restore: rdx becomes 0-7
Rem:
	iaddq $8, %rdx
	jle Done		# 0 elements, done
	iaddq $-4, %rdx		# rdx was 1-7, now -3 to 3
	jl R123			# 1-3 elements (rdx = -3 to -1)
	je R4			# 4 elements
	iaddq $-2, %rdx		# rdx was 5-7, now 1 to 3
	jl R5			# 5 elements
	je R6			# 6 elements

R7:
	mrmovq 48(%rdi), %r8
	rmmovq %r8, 48(%rsi)
	andq %r8, %r8
	jle R6
	iaddq $1, %rax
R6:
	mrmovq 40(%rdi), %r8
	rmmovq %r8, 40(%rsi)
	andq %r8, %r8
	jle R5
	iaddq $1, %rax
R5:
	mrmovq 32(%rdi), %r8
	rmmovq %r8, 32(%rsi)
	andq %r8, %r8
	jle R4
	iaddq $1, %rax
R4:
	mrmovq 24(%rdi), %r8
	rmmovq %r8, 24(%rsi)
	andq %r8, %r8
	jle R3
	iaddq $1, %rax
R3:
	mrmovq 16(%rdi), %r8
	rmmovq %r8, 16(%rsi)
	andq %r8, %r8
	jle R2
	iaddq $1, %rax
R2:
	mrmovq 8(%rdi), %r8
	rmmovq %r8, 8(%rsi)
	andq %r8, %r8
	jle R1
	iaddq $1, %rax
R1:
	mrmovq (%rdi), %r8
	rmmovq %r8, (%rsi)
	andq %r8, %r8
	jle Done
	iaddq $1, %rax
	jmp Done

R123:
	iaddq $2, %rdx		# rdx was -3 to -1, now -1 to 1
	jl R1			# 1 element
	je R2			# 2 elements
	jmp R3			# 3 elements

##################################################################
# Do not modify the following section of code
# Function epilogue.
Done:
	ret
##################################################################
# Keep the following label at the end of your function
End:
#/* $end ncopy-ys */
EndFun:

###############################
# Source and destination blocks 
###############################
	.align 8
src:
	.quad -1
	.quad 2
	.quad -3
	.quad 4
	.quad 0xbcdefa # This shouldn't get moved

	.align 16
Predest:
	.quad 0xbcdefa
dest:
	.quad 0xcdefab
	.quad 0xcdefab
	.quad 0xcdefab
	.quad 0xcdefab
Postdest:
	.quad 0xdefabc

.align 8
# Run time stack
	.quad 0
	.quad 0
	.quad 0
	.quad 0
	.quad 0
	.quad 0
	.quad 0
	.quad 0
	.quad 0
	.quad 0
	.quad 0
	.quad 0
	.quad 0
	.quad 0
	.quad 0
	.quad 0

Stack:
